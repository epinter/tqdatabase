/*
 * Copyright (C) 2021 Emerson Pinter - All Rights Reserved
 */

apply plugin: 'java-library'
apply plugin: 'java'
apply plugin: 'org.sonarqube'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.3"
    }
}

group = 'br.com.pinter.tqdatabase'
version = '0.5'

java {
    sourceCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.11.0-M2'
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")

    implementation 'org.apache.commons:commons-lang3:3.14.0'
    implementation 'net.java.dev.jna:jna-platform-jpms:5.14.0'
}

ext.moduleName = 'br.com.pinter.tqdatabase'

jar {
    //noinspection GroovyAssignabilityCheck
    manifest {
        inputs.property("moduleName", moduleName)
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Automatic-Module-Name': moduleName)
    }
}

test {
    useJUnitPlatform()
}

tasks.register('copyDeps', Copy) {
    dependsOn jar
    from configurations.runtimeClasspath
    into layout.buildDirectory.dir("deps")
}
tasks.jar.finalizedBy(copyDeps)

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}
